---
title: "`Stan`: Overview and Examples"
author: "Stephen Rhodes - srhodes@research.baycrest.org"
date: 'Last updated: `r Sys.Date()`'
output:
  ioslides_presentation:
    widescreen: yes
  highlight: tango
---

```{r setup, include=FALSE}
knitr::opts_chunk$set(echo = T)
```

<!--
## Things to cover in these slides...

- a basic stan model
- rstan
- a more complicated stan model (e.g. 2D mixture?)
- vectorizing/ speeding up programs?

-->

# Anatomy of a Stan model

## Anatomy of a Stan model

Necessary blocks (more info [here](https://mc-stan.org/docs/2_18/reference-manual/blocks-chapter.html))

```
data {
  \\ what we are modeling
}
parameters {
  \\ the variables being sampled by Stan
}
model {
  \\ priors and likelihood function
}
```

## Anatomy of a Stan model

Optional blocks (more info [here](https://mc-stan.org/docs/2_18/reference-manual/blocks-chapter.html))

```
functions {
  \\ user defined functions
}
transformed data {
  \\ as the name suggests
}
transformed parameters {
  \\ sometimes useful to speed up program
}
generated quantities {
  \\ does things with the samples 
  \\ e.g., calculate log likelihood, posterior predictions
}
```

## `sleepstudy` Example

```{r}
sleepstudy <- read.csv("../examples/sleepstudy.csv")
head(sleepstudy)
```

## `data`

```
data {
  int<lower=0> N;             // n observations
  vector[N] y;                // reaction times
  matrix[N,2] x;              // design matrix (intercept, days)
  int<lower=0> S;             // n subjects
  int<lower=1,upper=S> id[N]; // subject ids
}
```
More on [data types](https://mc-stan.org/docs/2_18/reference-manual/data-types-chapter.html)

## `parameters`

Below are the parameters for the simple (non-hierarchical) regression model (model 1)

```
parameters {
  vector[2] beta;             // fixed effects
  real<lower=0> sigma;        // residual SD
}
```

## `model`

```
model {
  // priors
  sigma ~ cauchy(0,50);
  beta[1] ~ normal(300,100);
  beta[2] ~ normal(0,50);
  // likelihood
  y ~ normal(x * beta, sigma);
}
```

- `~` sampling statement (is distributed as)
- `=` assignment

## `generated quantities`

```
generated quantities {
  vector[N] log_lik;      // log likelihood matrix
  vector[N] y_rep;        // posterior predictions
  for (i in 1:N){
    log_lik[i] = normal_lpdf(y[i] | x[i] * beta, sigma);
    y_rep[i] = normal_rng(x[i] * beta, sigma);
  }
}
```

- `log_lik`: log likelihood matrix. Used for calculating loo, waic
- `y_rep`: samples from the posterior predictive distribution

## `.stan`

- These blocks are all bundled into a `.stan` file
- See "examples/stan-models/example1-m1.stan" for this example program
- We can use the `rstan` package to get samples

# The `rstan` package

## `rstan`

Is the interface between `R` and `Stan`

Steps:

1. Put data into a list
2. Call the `stan()` function 
3. Do stuff with the samples

## Data list with `sleepstudy`

Everything in the `data` block should appear here

```r
data_list = list(
  N = nrow(sleepstudy),
  y = sleepstudy$Reaction,
  x = model.matrix(~ Days, data = sleepstudy),
  S = length(unique(sleepstudy$Subject)),
  id = as.numeric(sleepstudy$Subject) # ids should go from 1:S
)
```

```{r}
head(model.matrix(~ Days, data = sleepstudy))
```

## Run samples with `stan()`

```r
m1_fit <- stan(
  file = "examples/stan-models/example1-m1.stan",
  data = data_list,
  chains = 4,
  warmup = 1000,
  iter = 2000,
  cores = 4
)
```

This runs 4 separate chains for 2000 samples (each) and discards the first 1000 (4000 post-warmup samples total)

## Do stuff with the samples

- `plot(m1_fit, pars="beta")` plots posterior mean, 80% and 95% credible intervals
- `traceplot(m1_fit, pars="beta")` plots chains
- `yrep <- extract(m1_fit, pars = "y_rep")[[1]]` extract samples
- `bridge_sampler(m1_fit)` calculate marginal likelihood (using `bridgesampling` package)
- Leave-one-out cross-validation (using `loo` package)
    - `log_lik_1 = extract_log_lik(m1_fit, merge_chains = F)`
    - `r_eff_1 = relative_eff(exp(log_lik_1))`
    - `loo_1 = loo(log_lik_1, r_eff = r_eff_1)`

## stan-example1.R

Run through example script and .stan programs...

# A more complicated example

## 2D mixture model

```{r echo=F}

recall_2d = read.csv("../examples/recall-2D.csv")

```

```{r, echo=F}
par(pty='s', mar=c(1,1,1,1))
plot(NA, xlim=c(-15,15), ylim=c(-15,15), xlab="", ylab="", axes=F)

for (i in 1:100){
  with(recall_2d[i,], lines(c(px, rx), c(py, ry), lty=2, col="lightgrey"))
  with(recall_2d[i,], points(c(px, rx), c(py, ry), type='p', col=c("black", "red")))
}
legend("bottomleft", legend = c("Studied", "Recalled"), col = c("black", "red"), pch=1, bty='n')
par(pty='m', mar=c(5, 4, 4, 2) + 0.1)
```

## 2D mixture model

```{r, echo=F}
recall_2d$error = with(recall_2d, sqrt((px-rx)^2 + (py-ry)^2))

with(recall_2d, hist(error, breaks=30, col="lightgreen", border=F, xlab="Distance between studied and recalled", main="Recall Error", probability = T))

with(recall_2d, lines(density(error , from=0)))

```

## The model

```
Distribution of recalled locations = mixture of responses from memory and guesses
```

$$
\mathbf{r}_{ij} \sim m_i \times \phi(\mathbf{p}_j, s_i) + (1 - m_i) \times \frac{1}{A}
$$

- $i$ = Individuals; $j$ = trials 
- $\mathbf{r}$ = recalled location, $\mathbf{p}$ = presented (studied) location. 
- $m_i$ = probability of a response from memory for individual $i$
- $s_i$ = imprecision of memory for individual $i$
- $A$ = area where responses are allowed

## The model

$$
\mathbf{r}_{ij} \sim m_i \times \phi(\mathbf{p}_j, s_i) + (1 - m_i) \times \frac{1}{A}
$$

$$
\mbox{logit}(m_i) \sim \mbox{Normal}(\mu_m, \sigma_m) \\
\mbox{log}(s_i) \sim \mbox{Normal}(\mu_s, \sigma_s)
$$

We need priors for the population level parameters, $\mu_m$, $\mu_s$, $\sigma_m$, and $\sigma_s$

## The data

```{r}
head(recall_2d)
```

## example2.stan

```
data {
  int<lower=0> N;             // N observations
  int<lower=0> S;             // number of participants
  int<lower=1,upper=S> id[N]; // participant id
  vector[N] p_x;              // presented x coordinate
  vector[N] p_y;              // presented y coordinate
  vector[N] r_x;              // recalled x coordinate
  vector[N] r_y;              // recalled y coordinate
  real<lower=0> A;            // for determining uniform (guess) density
}
```

## example2.stan

```
parameters {
  real mu_m;                  // population mean for m (logit scale)
  real mu_s;                  // population mean for s (log scale)
  
  real<lower=0> sd_m;         // population sd for m (logit)
  real<lower=0> sd_s;         // population sd for s (log)
  
  vector[S] m;                // individual m parameters (logit)
  vector[S] s;                // individual s parameters (log)
}
```

$$
\mbox{logit}(m_i) \sim \mbox{Normal}(\mu_m, \sigma_m) \\
\mbox{log}(s_i) \sim \mbox{Normal}(\mu_s, \sigma_s)
$$

## example2.stan

```
model {
  // priors
  sd_m ~ cauchy(0,2.5);
  sd_s ~ cauchy(0,2.5);
  mu_m ~ normal(0,5);
  mu_s ~ normal(0,5);
  
  // sample individual parameters from population
  for (i in 1:S){
    m[i] ~ normal(mu_m, sd_m);
    s[i] ~ normal(mu_s, sd_s);
  }
... // continued on next slide
```

$$
\mbox{logit}(m_i) \sim \mbox{Normal}(\mu_m, \sigma_m) \\
\mbox{log}(s_i) \sim \mbox{Normal}(\mu_s, \sigma_s)
$$

## example2.stan

```
  // likelihood
  for (n in 1:N)
    target += log_mix(inv_logit(m[id[n]]),
                      normal_lpdf(r_x[n] | p_x[n], exp(s[id[n]])) + 
                      normal_lpdf(r_y[n] | p_y[n], exp(s[id[n]])),
                      log(1/A));
}
```

Not quite the same, but equivalent to...

$$
\mathbf{r}_{ij} \sim m_i \times \phi(\mathbf{p}_j, s_i) + (1 - m_i) \times \frac{1}{A}
$$

## stan-example2.R

Run through script
